def testExecutor

pipeline {
    agent any

    tools {
        allure 'allure'
    }

    environment {
        APP_NETWORK     = "application_messageboard-network"
        REST_TEST_IMAGE = "messageboard-rest-tests"

        ALLURE_REPORT   = "${WORKSPACE}/allure-report"
    }

    stages {
        stage('Initialise test executor') {
            steps {
                script {
                    testExecutor = load 'pipelines/scripts/testRunner.groovy'
                }
            }
        }
        stage('Build application images') {
            steps {
                dir("pipelines/application") {
                    sh 'docker compose build'
                }
            }
        }

        stage('Start application stack') {
            steps {
                dir("pipelines/application") {
                    sh 'docker compose up -d'
                }
            }
        }

        stage('Build Rest assured image') {
            steps {
                dir("pipelines/rest-assured") {
                    sh "docker build -t ${REST_TEST_IMAGE} ."
                }
            }
        }

        stage('Run integration Tests') {
            steps {
                script {
                    testExecutor.runTestsAndCollectAllure(
                            container: 'rest-tests-integration',
                            testSelector: 'IntegrationTest',
                            resultsDir: 'allure-results/integration',
                            network: APP_NETWORK,
                            image: REST_TEST_IMAGE
                    )
                }
            }
        }

        stage('Run e2e tests') {
            steps {
                script {
                    testExecutor.runTestsAndCollectAllure(
                            container: 'rest-tests-e2e',
                            testSelector: 'e2eTest',
                            resultsDir: 'allure-results/e2e',
                            network: APP_NETWORK,
                            image: REST_TEST_IMAGE
                    )
                }
            }
        }

        stage('Publish Allure report') {
            steps {
                archiveArtifacts artifacts: 'allure-results/**', fingerprint: true

                allure(
                        includeProperties: false,
                        jdk: '',
                        results: [
                                [path: 'allure-results/integration'],
                                [path: 'allure-results/e2e']
                        ]
                )
            }
        }

        stage('Send Slack notification') {
            steps {
                withCredentials([string(credentialsId: 'SLACK_WEBHOOK', variable: 'SLACK_WEBHOOK_URL')]) {
                    dir('pipelines/scripts') {
                        sh '''
                          export SLACK_WEBHOOK_URL="$SLACK_WEBHOOK_URL"
                          export ALLURE_REPORT_DIR="${ALLURE_REPORT}"
                          ./slack.sh
                        '''
                    }
                }
            }
        }
    }

    post {
        always {
            archiveArtifacts artifacts: 'allure-results/**, allure-report/**', fingerprint: true

            dir("pipelines/application") {
                sh 'docker compose down -v'
            }
        }

        failure {
            echo "Pipeline failed"
        }
    }
}
